plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'java'
    id 'antlr'
    id 'jacoco'
}

repositories {
    jcenter()
}

dependencies {
    antlr 'org.antlr:antlr4:4.7'
    compile 'org.ow2.sat4j:org.ow2.sat4j.core:2.3.5'
    compile 'org.apache.logging.log4j:log4j-api:2.9.1'
    compile 'org.apache.logging.log4j:log4j-core:2.9.1'
    compile 'org.eclipse.collections:eclipse-collections-api:9.0.0'
    compile 'org.eclipse.collections:eclipse-collections:9.0.0'
    compile 'org.apache.commons:commons-text:1.1'
    compile 'commons-cli:commons-cli:1.4'
    testCompile 'com.mscharhag.oleaster:oleaster-runner:0.2.0'
    testCompile 'com.mscharhag.oleaster:oleaster-matcher:0.2.0'
    testCompile 'junit:junit:4.12'
}

generateGrammarSource {
    exclude 'generated/Automaton.g4'
    exclude 'generated/Transducer.g4'
    exclude 'generated/Tokens.g4'
    arguments += ["-lib", "src/main/antlr/generated"]
}

test {
    testLogging {
        showStandardStreams = true
    }
}

jacoco {
    toolVersion = '0.7.9'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

jar.enabled = false

shadowJar {
    destinationDir = projectDir
    classifier = null
}

assemble.dependsOn shadowJar

clean.doLast {
    file("${projectDir}/${rootProject.name}.jar").delete()
}
