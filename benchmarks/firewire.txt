// firewire.txt
//
// An abstraction of root-contention stage of firewire protocol as described
// in "Proving termination in B" by McIver et al. 2003 and also in
// Esparza et al. 2012.
//
// Here, we give a transducer representation of it as an example of a token
// passing in a linear array with one token, where the token can
// probabilistically stay or move to the right.
//
// T - token
// N - no token
//

// the token is on the left end
I0 {
    init: s0;

    s0 -> s1 T;
    s1 -> s1 N;

    accepting: s1;
}

closedUnderTransitions;

// the token is on the right end
F {
    init: s0;

    s0 -> s0 N;
    s0 -> s1 T;

    accepting: s1;
}

P1 {
    init: s0;

    s0 -> s0 N/N;
    s0 -> s1 T/T_;

    s1 -> s1 N/N;

    accepting: s1;
}

P2 {
    init: s0;

    s0 -> s0 N/N;

    s0 -> s1 T_/N;
    s1 -> s2 N/T;
    s2 -> s2 N/N;

    s0 -> p1 T_/T;

    accepting: s2, p1;
}

transducerStateGuessing: 1 .. 10;
automatonStateGuessing: 1 .. 4;
